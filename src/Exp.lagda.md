Defines the *terms* for the Proof Trees to act on.

### To add a new term
- First add to the Exp type below
- Then modify the corresponding cases in map-var-to-var and map-env
See their definitions for more details.

```agda
{-# OPTIONS --without-K --safe #-}

module Exp where

open import Data.String using (String)
open import Data.List using (List ; _âˆ·_ ; [])

infix 9 _â—ƒ_
infix 5 Î _ê‰_,_
infix 5 Î»Ì£_,_
infix 5 Î£_ê‰_,_
infix 5 Ïƒ_,_

```
Env tracks all variables that a term may reference. (They may come from the surronding Context (Î“) or expressions like Î  / Î£ / Î»Ì£)

This does allow multiple unique variables to have the same name. But, they will be distinguished when refering to them. Each references (VarIn) refers to a specific variable by index (De Bruijn style) into their environment. (See [ExpShorthand](ExpShorthand.lagda.md) for easy ways to refer to a variable.)
```agda
Env = List String

data VarIn : Env â†’ Set where
  CurrVar : {rst : Env} â†’ (v : String) â†’ VarIn (v âˆ· rst)
  NextVar
    : {v : String} {rst : Env} â†’ VarIn rst â†’ VarIn (v âˆ· rst)

data Exp : Env â†’ Set where
  Var : {e : Env} â†’ VarIn e â†’ Exp e
  ğŸ˜ : {e : Env} â†’ Exp e
  ğŸ™ : {e : Env} â†’ Exp e
  â‹† : {e : Env} â†’ Exp e
  â„• : {e : Env} â†’ Exp e
  ğŸ : {e : Env} â†’ Exp e
  S : {e : Env} â†’ Exp e â†’ Exp e
  ind-â„•
    : {e : Env} 
    â†’ (pz : Exp e)
    â†’ {n prev : String}
    â†’ (ps : Exp (prev âˆ· n âˆ· e))
    â†’ (n : Exp e)
    â†’ Exp e
  Î _ê‰_,_ : {e : Env} â†’ (v : String) â†’ Exp e â†’ Exp (v âˆ· e) â†’ Exp e
  Î»Ì£_,_ : {e : Env} â†’ (v : String) â†’ Exp (v âˆ· e) â†’ Exp e
  _â—ƒ_ : {e : Env} (func : Exp e) â†’ (arg : Exp e) â†’ Exp e
  Î£_ê‰_,_ : {e : Env} â†’ (v : String) â†’ Exp e â†’ Exp (v âˆ· e) â†’ Exp e
  Ïƒ_,_ : {e : Env} â†’ Exp e â†’ Exp e â†’ Exp e

within-var
  : {a : String} {e e' : Env}
  â†’ (VarIn e â†’ VarIn e')
  â†’ VarIn (a âˆ· e) â†’ VarIn (a âˆ· e')
within-var f = Î» where
  (CurrVar v) â†’ CurrVar v
  (NextVar x) â†’ NextVar (f x)

```
map-var-to-var and map-env provide generic manipulation of variables in any term. Like common variable substitution or renaming.

They are nearly identical, but map-var-to-var exists to provide a clear terminating definition for Agda.

Most new terms can be added with a similar style case to the ones that already exist, by mapping their nested elements (if any) with the same function. Notice when their elements assume additional variables in their environment, then use the utilities within-var/within-var-to-env to *skip over* those vars. See Î  as an example.
```agda
map-var-to-var
  : {oldEnv newEnv : Env}
  â†’ (VarIn oldEnv â†’ VarIn newEnv)
  â†’ Exp oldEnv
  â†’ Exp newEnv
map-var-to-var f = Î» where
  (Var x) â†’ Var (f x)
  ğŸ˜ â†’ ğŸ˜
  ğŸ™ â†’ ğŸ™
  â‹† â†’ â‹†
  â„• â†’ â„•
  ğŸ â†’ ğŸ
  (S e) â†’ S (map-var-to-var f e)
  (ind-â„• e eâ‚ eâ‚‚) â†’
    ind-â„•
      (map-var-to-var f e)
      (map-var-to-var (within-var (within-var f)) eâ‚)
      (map-var-to-var f eâ‚‚)
  (Î  v ê‰ e , eâ‚) â†’
    Î  v ê‰ map-var-to-var f e , map-var-to-var (within-var f) eâ‚
  (Î»Ì£ v , e) â†’
    Î»Ì£ v , map-var-to-var (within-var f) e
  (e â—ƒ eâ‚) â†’ map-var-to-var f e â—ƒ map-var-to-var f eâ‚
  (Î£ v ê‰ e , eâ‚) â†’
    Î£ v ê‰ map-var-to-var f e , map-var-to-var (within-var f) eâ‚
  (Ïƒ e , eâ‚) â†’ Ïƒ map-var-to-var f e , map-var-to-var f eâ‚

within-var-to-env
  : {x : String} {oldEnv newEnv : Env}
  â†’ (VarIn oldEnv â†’ Exp newEnv)
  â†’ VarIn (x âˆ· oldEnv) â†’ Exp (x âˆ· newEnv)
within-var-to-env f = Î» where
  (CurrVar v) â†’ Var (CurrVar v)
  (NextVar rst) â†’ map-var-to-var NextVar (f rst)

map-env
  : {oldEnv newEnv : Env}
  â†’ (f : VarIn oldEnv â†’ Exp newEnv)
  â†’ Exp oldEnv
  â†’ Exp newEnv
map-env f = 
  Î» where
    (Var x) â†’ f x
    ğŸ˜ â†’ ğŸ˜
    ğŸ™ â†’ ğŸ™
    â‹† â†’ â‹†
    â„• â†’ â„•
    ğŸ â†’ ğŸ
    (S e) â†’ S (map-env f e)
    (ind-â„• e eâ‚ eâ‚‚) â†’
      ind-â„•
        (map-env f e)
        (map-env (within-var-to-env (within-var-to-env f)) eâ‚)
        (map-env f eâ‚‚)
    (Î  v ê‰ e , eâ‚) â†’ Î  v ê‰ map-env f e , map-env (within-var-to-env f) eâ‚
    (Î»Ì£ v , e) â†’ Î»Ì£ v , map-env (within-var-to-env f) e
    (e â—ƒ eâ‚) â†’ map-env f e â—ƒ map-env f eâ‚
    (Î£ v ê‰ e , eâ‚) â†’ Î£ v ê‰ map-env f e , map-env (within-var-to-env f) eâ‚
    (Ïƒ e , eâ‚) â†’ Ïƒ map-env f e , map-env f eâ‚
```
